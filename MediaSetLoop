page 50187 "ASI Item Picture"
{
    Caption = 'Item Picture';
    DeleteAllowed = false;
    InsertAllowed = false;
    LinksAllowed = false;
    PageType = CardPart;
    SourceTable = Item;

    layout
    {
        area(content)
        {
            field(Picture; TempBlobField."ASI Important")
            {
                ApplicationArea = All;
                ShowCaption = false;
                ToolTip = 'Specifies the picture that has been inserted for the item.';
            }
        }
    }

    actions
    {
        area(processing)
        {
            action(TakePicture)
            {
                ApplicationArea = All;
                Caption = 'Take';
                Image = Camera;
                ToolTip = 'Activate the camera on the device.';
                Visible = CameraAvailable AND (HideActions = FALSE);

                trigger OnAction()
                begin
                    TakeNewPicture();
                end;
            }
            action(ImportPicture)
            {
                ApplicationArea = All;
                Caption = 'Import';
                Image = Import;
                ToolTip = 'Import a picture file.';
                Visible = HideActions = FALSE;

                trigger OnAction()
                begin
                    ImportFromDevice();
                end;
            }
            action(ExportFile)
            {
                ApplicationArea = All;
                Caption = 'Export';
                Enabled = DeleteExportEnabled;
                Image = Export;
                ToolTip = 'Export the picture to a file.';
                Visible = HideActions = FALSE;

                trigger OnAction()
                var
                    DummyPictureEntity: Record "Picture Entity";
                    FileManagement: Codeunit "File Management";
                    StringConversionManager: Codeunit StringConversionManagement;
                    ToFile: Text;
                    ConvertedCodeType: Text;
                    ExportPath: Text;
                begin
                    TestField("No.");
                    TestField(Description);
                    ConvertedCodeType := Format("No.");
                    ToFile := DummyPictureEntity.GetDefaultMediaDescription(Rec);
                    ConvertedCodeType := StringConversionManager.RemoveNonAlphaNumericCharacters(ConvertedCodeType);
                    ExportPath := TemporaryPath + ConvertedCodeType + Format(Picture.MediaId);
                    Picture.ExportFile(ExportPath + '.' + DummyPictureEntity.GetDefaultExtension());

                    FileManagement.ExportImage(ExportPath, ToFile);
                end;
            }
            action(DeletePicture)
            {
                ApplicationArea = All;
                Caption = 'Delete';
                Enabled = DeleteExportEnabled;
                Image = Delete;
                ToolTip = 'Delete the record.';
                Visible = HideActions = FALSE;

                trigger OnAction()
                begin
                    DeleteItemPicture();
                end;
            }
            action(ShowNext)
            {
                ApplicationArea = all;
                Caption = 'Show Next Picture';
                ToolTip = 'Show the next picture of this item';
                Image = NextRecord;
                Enabled = ShouldShowNext;

                trigger OnAction()
                var
                begin
                    if CurrentImageNo <> ImageCount then
                        CurrentImageNo += 1
                    else
                        CurrentImageNo := 1;

                    if TenantMedia.Get(Picture.Item(CurrentImageNo)) then begin
                        TenantMedia.CalcFields(Content);
                        Clear(TempBlobField."ASI Important");
                        Clear(TempBlobField.Id);
                        TempBlobField."ASI Important" := TenantMedia.Content;
                        TempBlobField.Id := TenantMedia.ID;
                        TempBlobField.Modify();
                    end;
                    CurrPage.Update();
                end;
            }
        }
    }

    trigger OnAfterGetCurrRecord()
    begin
        SetEditableOnPictureActions();
    end;

    trigger OnAfterGetRecord()
    var
        i: Integer;
    begin
        if (not TempBlobField.Get()) or DeleteOccured then begin
            if DeleteOccured then begin
                DeleteOccured := false;
                if TempBlobField.IsTemporary then
                    TempBlobField.Delete();
                Clear(TempBlobField);
            end;
            TempBlobField.Insert();
            ImageCount := Picture.Count;
            ShouldShowNext := ImageCount > 1;

            if ImageCount > 0 then begin
                if TenantMedia.Get(Picture.Item(1)) then
                    if TenantMedia.CalcFields(Content) then begin
                        TempBlobField."ASI Important" := TenantMedia.Content;
                        TempBlobField.Id := Picture.Item(1);
                        TempBlobField.Modify();
                        CurrentImageNo := 1;
                    end;
            end;
        end else
            if TempBlobField."ASI Important".HasValue then
                TempBlobField.CalcFields("ASI Important");
    end;

    trigger OnOpenPage()
    begin
        CameraAvailable := Camera.IsAvailable();
    end;

    procedure TakeNewPicture()
    begin
        Rec.Find();
        Rec.TestField("No.");
        Rec.TestField(Description);

        DoTakeNewPicture();
    end;

    [Scope('OnPrem')]
    procedure ImportFromDevice()
    var
        FileManagement: Codeunit "File Management";
        FileName: Text;
        ClientFileName: Text;
    begin
        Find();
        TestField("No.");
        if Description = '' then
            Error(MustSpecifyDescriptionErr);

        if Picture.Count > 0 then
            if not Confirm(OverrideImageQst) then
                Error('');

        ClientFileName := "No.";
        FileName := FileManagement.UploadFile(SelectPictureTxt, ClientFileName);
        if FileName = '' then
            Error('');

        Picture.ImportFile(FileName, ClientFileName);
        Modify(true);

        if FileManagement.DeleteServerFile(FileName) then;

        DeleteOccured := true;

        CurrPage.Update();
    end;

    local procedure DoTakeNewPicture(): Boolean
    var
        PictureInstream: InStream;
        PictureDescription: Text;
    begin
        if Rec.Picture.Count() > 0 then
            if not Confirm(OverrideImageQst) then
                exit(false);

        if Camera.GetPicture(PictureInstream, PictureDescription) then begin
            Clear(Rec.Picture);
            Rec.Picture.ImportStream(PictureInstream, PictureDescription, MimeTypeTok);
            Rec.Modify(true);
            exit(true);
        end;

        exit(false);
    end;

    local procedure SetEditableOnPictureActions()
    begin
        DeleteExportEnabled := Picture.Count <> 0;
    end;

    procedure IsCameraAvailable(): Boolean
    begin
        exit(Camera.IsAvailable());
    end;

    procedure SetHideActions()
    begin
        HideActions := true;
    end;

    procedure DeleteItemPicture()
    var
        Item: Record Item;
    begin
        TestField("No.");

        if not Confirm(DeleteImageQst) then
            exit;

        if not IsNullGuid(TempBlobField.Id) then begin
            Item.Get(Rec."No.");
            Item.Picture.Remove(TempBlobField.Id);
            Item.Modify();

            if TenantMedia.Get(TempBlobField.Id) then
                TenantMedia.Delete();
            DeleteOccured := true;
        end;
    end;

    var
        TempBlobField: Record "Company Information" temporary;
        TenantMedia: Record "Tenant Media";

        Camera: Codeunit Camera;

        [InDataSet]
        CameraAvailable: Boolean;
        ShouldShowNext: Boolean;
        DeleteExportEnabled: Boolean;
        HideActions: Boolean;
        DeleteOccured: Boolean;

        OverrideImageQst: Label 'The existing picture will be replaced. Do you want to continue?';
        DeleteImageQst: Label 'Are you sure you want to delete the picture?';
        SelectPictureTxt: Label 'Select a picture to upload';
        MustSpecifyDescriptionErr: Label 'You must add a description to the item before you can import a picture.';
        MimeTypeTok: Label 'image/jpeg', Locked = true;

        ImageCount: Integer;
        CurrentImageNo: Integer;

}
